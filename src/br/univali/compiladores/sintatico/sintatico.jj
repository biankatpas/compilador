options{
    STATIC = false;
    IGNORE_CASE = false;
}

PARSER_BEGIN(Parser)
package br.univali.compiladores.sintatico;
import java.util.ArrayList;
import java.util.List;
import java.io.StringReader;

public class Parser {
	int contParseError = 0;
	boolean debugRecovery = true;
        final static List<Message> errorLexList = new ArrayList<Message>();
        final static List<Message> output = new ArrayList<Message>();

        public Parser() {

        };

        public List<Message> lexicalAnalizer(String code){
            Parser lexicalParser = new Parser();

            try{
                lexicalParser = new Parser(new StringReader(code));
                lexicalParser.program_lexico();
            } catch(Exception e){
                errorLexList.add(new Message(e.toString(), true));
            } 
                        
            List<Message> auxList = new ArrayList<Message>(errorLexList);
            errorLexList.clear();
            return auxList;
        }
        
            public List<Message> sintaticalAnalizer(String code){
            Parser lexicalParser = new Parser();

            try{
                lexicalParser = new Parser(new StringReader(code));
                lexicalParser.program_sintatico();
            } catch(Exception e){
                output.add(new Message(e.toString(), true));
            }
            
            List<Message> auxList = new ArrayList<Message>(output);
            output.clear();
            return auxList;
        }

	public void setDebugRecovery(boolean debugRecovery) {
		this.debugRecovery = debugRecovery;
	}

	static public String im(int x) {
		int k;
		String s;
		s = tokenImage[x];
		k = s.lastIndexOf("\"");
		try {
			s = s.substring(1, k);
		} catch (StringIndexOutOfBoundsException e) {

		}
		return s;
	}

	boolean eof;
	void consumeUntil(RecoverySet g,
					  ParseException e,
					  String met) throws ParseException {
		
		Token tok;
                
		if (debugRecovery) {
			System.out.println();
			System.out.println("*** " + met + " ***");
			System.out.println("Synchronizing Set: " + g);
		}

		if (g == null) throw e;

		tok = getToken(1);
		
		while (!eof) {
			if (g.contains(tok.kind)) {
				if (debugRecovery) {
					System.out.println("Found synchronizing token: " + im(tok.kind));
				}
				break;
			}
			if (debugRecovery) {
				System.out.println("Ignoring token: " + im(tok.kind));
			}

			getNextToken();
			tok = getToken(1);
			
			if (tok.kind == EOF && !g.contains(EOF)) {
				eof = true;
			}
		}
		if (debugRecovery) {
			System.out.println(e.getMessage());
		}
		// FILTRO PARA NAO CONTAR ERROS LEXICOS COMO SINTATICOS
                if (!errorLexList.contains(e.currentToken.next.kind)) {
                    contParseError++;
                }
		if (eof) throw new ParseException("EOF Found prematurely.");
	}
}
PARSER_END(Parser)

TOKEN_MGR_DECLS : {
    int countLexError = 0;

    public int foundLexError() {
        return countLexError;
    }   
}

JAVACODE int program_lexico() {
	Token t;
        String tokenType = "";
        Message m;

	do {
		t = getNextToken();
		Token st = t;
		while ( st.specialToken != null) {
                    st = st.specialToken;
		}
                

		do {
                switch (t.kind) {
                    case ParserConstants.IDENTIFICADOR_INVALIDO:
                        m = new Message("Error "+ st.kind+": Identificador Invalido em linha " + jj_input_stream.getBeginLine() + ", coluna " + jj_input_stream.getBeginColumn() + ".", true);
                        if (!errorLexList.contains(m)){
                            errorLexList.add(m);
                        }
                        break;
                     case ParserConstants.SIMBOLO_INVALIDO:
                        m = new Message("Error "+ st.kind+": Simbolo invalido em linha " + jj_input_stream.getBeginLine() + ", coluna " + jj_input_stream.getBeginColumn() + ".", true);
                        if (!errorLexList.contains(m)){
                            errorLexList.add(m);
                        }
                        break;    
                    case ParserConstants.CONSTANTE_NUM_INTEIRA_INVALIDA:
                        m = new Message("Error "+ st.kind+": Constante numerica inteira invalida em linha " + jj_input_stream.getBeginLine() + ", coluna " + jj_input_stream.getBeginColumn() + ".", true);
                        if (!errorLexList.contains(m)){
                            errorLexList.add(m);
                        }
                        break;
                    case ParserConstants.CONSTANTE_NUM_REAL_INVALIDA:
                        m = new Message("Error "+ st.kind+": Constante numerica real invalida em linha " + jj_input_stream.getBeginLine() + ", coluna " + jj_input_stream.getBeginColumn() + ".", true);
                        if (!errorLexList.contains(m)){
                            errorLexList.add(m);
                        }
                        break;
                    case ParserConstants.CONSTANTE_LITERAL_INVALIDA:
                        m = new Message("Error "+ st.kind+": Constante literal invalida em linha " + jj_input_stream.getBeginLine() + ", coluna " + jj_input_stream.getBeginColumn() + ".", true);
                        if (!errorLexList.contains(m)){
                            errorLexList.add(m);
                        }
                        break;
                    default:
                        break;

                }
                st = st.next;

            } while (st != t.next);

        } while (t.kind != ParserConstants.EOF);
    return output.size();    
}

void program_sintatico() : {
    RecoverySet r = new RecoverySet(EOF);
} {
    try {
        [ begin_program(r) ]<EOF>
    } catch (ParseException e) {
        consumeUntil(r, e, "program_sintatico");
        output.add(new Message("Erro: forma geral do programa.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void type() : {}
{
	<INTEGER> | <REAL> | <STRING> | <LOGIC>
}

void value() : {} 
{
	<CONSTANTE_NUM_INTEIRA> | <CONSTANTE_NUM_REAL> | <CONSTANTE_LITERAL>
}

void begin_program(RecoverySet r): 
{
    RecoverySet h = new RecoverySet(BODY);
    RecoverySet g = new RecoverySet(DECLARATION);
    RecoverySet i = g.union(h);
    RecoverySet l = new RecoverySet(DESCRIPTION);
    
}
{
    try{
        header(i)
        declarations(h)
        body(r.union(l))
        description(r)
    } catch (ParseException e) {
        consumeUntil(r, e, "begin_program");
        output.add(new Message("Erro: forma geral do programa.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void header(RecoverySet r) : {} {
    try {
        <DO> <THIS> <IDENTIFICADOR> <OPENBRACKET> <CLOSEBRACKET>
    } catch (ParseException e) {
        consumeUntil(r, e, "header");
        output.add(new Message("Erro: forma geral do programa.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void description(RecoverySet r):
{}
{
    try {
         ( <DESCRIPTION> <CONSTANTE_LITERAL>)? 
    } catch (ParseException e) {
        consumeUntil(r, e, "description");
        output.add(new Message("Erro: description comentario.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void declarations(RecoverySet r):
{}
{
    try {
        ( <DECLARATION> declarations_(r) ) ?
    } catch (ParseException e) {
        consumeUntil(r, e, "declarations");
        output.add(new Message("Erro: declarations.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void declarations_(RecoverySet r):
{}
{
    try{
        (declaracao_tipo_enumerado() declaracao_tipo_enumerado_()) | declaracao_constantes_variaveis()
    } catch (ParseException e) {
        consumeUntil(r, e, "declarations_");
        output.add(new Message("Erro: declarations.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void declaracao_tipo_enumerado():
{}
{
    <TYPE> <OPENBRACKET> identificadores_constantes() <DOT> <CLOSEBRACKET>
}

void declaracao_tipo_enumerado_():
{}
{
    ( <DECLARATION> declaracao_constantes_variaveis() )?
}

void identificadores_constantes():
{}
{
    try {
        constante() identificadores_constantes_()
    } catch (ParseException e) {
        output.add(new Message("Erro: identificadores_constantes.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void identificadores_constantes_():
{}
{
    try {
        (identificadores_constantes())?
    } catch (ParseException e) {
        output.add(new Message("Erro: identificadores_constantes.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void constante():
{}
{
    try {
        <IDENTIFICADOR> <IS> lista_identificadores_constante()
    } catch (ParseException e) {
        output.add(new Message("Erro: constante.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void lista_identificadores_constante():
{}
{
    try {
        <IDENTIFICADOR> lista_identificadores_constante_()
    } catch (ParseException e) {
        output.add(new Message("Erro: lista_identificadores_constantes.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void lista_identificadores_constante_():
{}
{
    try {
        (<COMMA> <IDENTIFICADOR> lista_identificadores_constante__())?
    } catch (ParseException e) {
        output.add(new Message("Erro: lista_identificadores_constantes.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void lista_identificadores_constante__():
{}
{
    try {
        lista_identificadores_constante_()
    } catch (ParseException e) {
        output.add(new Message("Erro: lista_identificadores_constantes.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void declaracao_constantes_variaveis():
{}
{
    <CONSTANT> <AND> <VARIABLE> <OPENBRACKET> constantes_variaveis() <CLOSEBRACKET>
}

void constantes_variaveis():
{}
{
    try {
        (<AS> constantes_variaveis_())?
    } catch (ParseException e) {
        output.add(new Message("Erro: constantes_variaveis.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void constantes_variaveis_():
{}
{
    try {
        ( bloco_constante() constante_() ) | ( bloco_variavel() variavel_() )
    } catch (ParseException e) {
        output.add(new Message("Erro: constantes_variaveis.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void constante_():
{}
{
    try {
        ( <AS> bloco_variavel() ) ?
    } catch (ParseException e) {
        output.add(new Message("Erro: constantes.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void variavel_():
{}
{
    try {
        ( <AS> bloco_constante() ) ?
    } catch (ParseException e) {
        output.add(new Message("Erro: variaveis.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void bloco_constante():
{}
{
    try {
        <CONSTANT> declaracao_constante() bloco_constante_() <DOT>
    } catch (ParseException e) {
        output.add(new Message("Erro: bloco_constante.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void bloco_constante_():
{}
{
    try {
        ( declaracao_constante() ) *
    } catch (ParseException e) {
        output.add(new Message("Erro: bloco_constante.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void declaracao_constante():
{}
{
    try {
        lista_identificadores_constante() <IS> type() <ASSIGNMENT> value() 
    } catch (ParseException e) {
        output.add(new Message("Erro: declaracao_constante.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void bloco_variavel():
{}
{
    try {
        <VARIABLE> declaracao_variavel() bloco_variavel_() <DOT>
    } catch (ParseException e) {
        output.add(new Message("Erro: bloco_variavel.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void bloco_variavel_():
{}
{
    try {
        ( declaracao_variavel() ) *
    } catch (ParseException e) {
        output.add(new Message("Erro: bloco_variavel.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void declaracao_variavel():
{}
{
    try {
        lista_identificadores_variavel() <IS> type()
    } catch (ParseException e) {
        output.add(new Message("Erro: declaracao_variavel.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void lista_identificadores_variavel():
{}
{
    try {
        <IDENTIFICADOR> lista_identificadores_variavel_()
    } catch (ParseException e) {
        output.add(new Message("Erro: lista_identificadores_variavel.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void lista_identificadores_variavel_():
{}
{
    try {
        ( <COMMA> <IDENTIFICADOR>
        lista_identificadores_variavel__() |
        <OPENBRACKET><CONSTANTE_NUM_INTEIRA><CLOSEBRACKET> lista_identificadores_variavel__()
     ) ?
    } catch (ParseException e) {
        output.add(new Message("Erro: lista_identificadores_variavel.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void lista_identificadores_variavel__():
{}
{
    try {
        lista_identificadores_variavel_()
    } catch (ParseException e) {
        output.add(new Message("Erro: lista_identificadores_variavel.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void body(RecoverySet r):
{
     RecoverySet g = new RecoverySet(OPENBRACKET);
}
{
    try {
        <BODY> <OPENBRACKET> lista_comandos(g) <CLOSEBRACKET>
    } catch (ParseException e) {
        consumeUntil(r, e, "body");    
        output.add(new Message("Erro: body.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void lista_comandos(RecoverySet r):
{
    RecoverySet g = First.lista_comandos;
}
{
    try {
        (comando_atribuicao(g) | comando_entrada_dados(g) | comando_saida_dados(g) | comando_selecao(g) |  comando_repeticao(g) )+
  } catch (ParseException e) {
        consumeUntil(r, e, "lista_comandos");
        output.add(new Message("Erro: lista_comandos.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void comando_atribuicao(RecoverySet r):
{
    RecoverySet g = new RecoverySet(TO);
    RecoverySet h = new RecoverySet(DOT);
}
{
    try {
        <DESIGNATE> <THIS> lista_identificadores_variavel() <TO> expression(h) <DOT>
    } catch (ParseException e) {
         consumeUntil(r, e, "comando_atribuicao");
        output.add(new Message("Erro: comando_atribuicao.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void comando_entrada_dados(RecoverySet r):
{}
{
    try {
        <READ> <THIS> <OPENBRACKET> lista_identificadores_variavel() <CLOSEBRACKET> <DOT>
    } catch (ParseException e) {
        consumeUntil(r, e, "comando_entrada_dados");
        output.add(new Message("Erro: comando_entrada_dados.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void comando_saida_dados(RecoverySet r):
{
     RecoverySet g = new RecoverySet(DOT);
}
{
    try {
        <WRITE> comando_saida_dados_(g) <DOT>
    } catch (ParseException e) {
         consumeUntil(r, e, "comando_saida_dados");
        output.add(new Message("Erro: comando_saida_dados.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void comando_saida_dados_(RecoverySet r):
{}
{
    try {
        (<ALL> comando_saida_dados__()) | comando_saida_dados__()
  } catch (ParseException e) {
        consumeUntil(r, e, "comando_saida_dados_");
        output.add(new Message("Erro: comando_saida_dados.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void comando_saida_dados__():
{}
{
    try {
        <THIS> <OPENBRACKET> lista_identificadores_constantes() <CLOSEBRACKET>
  } catch (ParseException e) {
        output.add(new Message("Erro: comando_saida_dados.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void lista_identificadores_constantes() : {} {
    <IDENTIFICADOR>(<OPENBRACKET><CONSTANTE_NUM_INTEIRA><CLOSEBRACKET>)?  lista_identificadores_constantes_() |
    <CONSTANTE_NUM_INTEIRA> lista_identificadores_constantes_()|
    <CONSTANTE_NUM_REAL> lista_identificadores_constantes_()|
    <CONSTANTE_LITERAL>lista_identificadores_constantes_()
}

void lista_identificadores_constantes_() : {} {
    (<COMMA> lista_identificadores_constantes())?
}

void comando_selecao(RecoverySet r):
{
    RecoverySet g = new RecoverySet(DOT);
}
{
    try {
        <AVALIATE> <THIS> expression(First.comando_selecao_) comando_selecao_(g) <DOT>
    } catch (ParseException e) {
        consumeUntil(r, e, "comando_selecao");
        output.add(new Message("Erro: comando_selecao.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void comando_selecao_(RecoverySet r):
{}
{
    try {
        selecao_true() | selecao_untrue()
    } catch (ParseException e) {
        consumeUntil(r, e, "comando_selecao_");
        output.add(new Message("Erro: comando_selecao.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void selecao_true():
{
    RecoverySet g = new RecoverySet(CLOSEBRACKET);
}
{
    try {
        <TRUE> <RESULT> <OPENBRACKET> lista_comandos(g) <CLOSEBRACKET> (selecao_untrue())?
    } catch (ParseException e) {
        output.add(new Message("Erro: comando_selecao.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void selecao_untrue():
{
    RecoverySet g = new RecoverySet(CLOSEBRACKET);
}
{
    try {
        <UNTRUE> <RESULT> <OPENBRACKET> lista_comandos(g) <CLOSEBRACKET> (selecao_true())?
    } catch (ParseException e) {
        output.add(new Message("Erro: comando_selecao.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void comando_repeticao(RecoverySet r) :
{
    RecoverySet g = new RecoverySet(OPENBRACKET);
    RecoverySet h = new RecoverySet(CLOSEBRACKET);
}
{
    try {
        <REPEAT> <THIS> expression(g) <OPENBRACKET> lista_comandos(h) <CLOSEBRACKET> <DOT>
  } catch (ParseException e) {
        consumeUntil(r, e, "comando_repeticao");
        output.add(new Message("Erro: comando_repeticao.\n"+ErrorMessage.getErrorMessage(e), true));
    }
}

void expression(RecoverySet g) : {} 
{	
	try {
		expression_arith_or_logic(g) expression_(g)
	} catch (ParseException e) {
		consumeUntil(g, e, "expression");
                output.add(new Message("Erro: expressao.\n"+ErrorMessage.getErrorMessage(e), true));
	}
}

void expression_(RecoverySet g) : {}
{
	<EQUALS> expression_arith_or_logic(g) |
	<DIFFERENT> expression_arith_or_logic(g) |
	<LESS> expression_arith_or_logic(g) |
	<GREATER> expression_arith_or_logic(g) |
	<LESSEQUAL> expression_arith_or_logic(g) |
	<GREATEREQUAL> expression_arith_or_logic(g) |
	{}
}

void expression_arith_or_logic(RecoverySet g) : {}
{
	term2(g) less_priority(g)
}

void less_priority(RecoverySet g) : {} 
{
	<PLUS> term2(g) less_priority(g) |
	<MINUS> term2(g) less_priority(g) |
	<PIPE> term2(g) less_priority(g) |
	{}
}

void term2(RecoverySet g) : {}
{
	term1(g) med_priority(g)
}

void med_priority(RecoverySet g) : {}
{
	<MULTIPLY> term1(g) med_priority(g) |
	<FRACTION> term1(g) med_priority(g) |
	<PERCENT> term1(g) med_priority(g) |
	<MOD> term1(g) med_priority(g) |
	<AMPERSAND> term1(g) med_priority(g) |
	{}
}

void term1(RecoverySet g) : {}
{
	element(g) high_priority()
}

void high_priority() : {}
{
	<POTENCY> high_priority() |
	{}
}

void element(RecoverySet g) : {} 
{
	<IDENTIFICADOR> index() |
	<CONSTANTE_NUM_INTEIRA> |
	<CONSTANTE_NUM_REAL> |
	<CONSTANTE_LITERAL> |
	<TRUE> |
	<UNTRUE> |
	<OPENPARENTHESIS> expression(g) <CLOSEPARENTHESIS> |
	<EXCLAMATION> <OPENPARENTHESIS> expression(g) <CLOSEPARENTHESIS>
}

void index() : {} 
{
	LOOKAHEAD(2) <OPENBRACKET> <CONSTANTE_NUM_INTEIRA> <CLOSEBRACKET> |
	{}
}

//------------------------------------------------------------- lexico - m1 - inicio ------------------------------------------------------------------
//CHARACTERS TO SCAPE

SKIP : {
          " "
	| "\t"
        | "\n"
        | "\r"
        | "\f"
}

SKIP : {
   "##": multilinecomment
}

SKIP : {
    "//": singlelinecomment
}

<multilinecomment> SKIP : {
	"$$": DEFAULT
	| <~[]>
}

<singlelinecomment> SKIP : {
	<["\n", "\r"]> : DEFAULT
	| <~[]>
}

//INTEGER / FLOAT TOKENS
TOKEN : {
    <CONSTANTE_NUM_INTEIRA: <DIGIT>(<DIGIT>)?(<DIGIT>)?(<DIGIT>)?>
	| <CONSTANTE_NUM_REAL: <DIGIT>(<DIGIT>)?(<DIGIT>)?(<DIGIT>)?(<DIGIT>)?<DOT><DIGIT>(<DIGIT>)?(<DIGIT>)?>
        //| <CONSTANTE_NUM_REAL: >
	| <#DIGIT: ["0"-"9"]>
}

//RESERVED WORDS
TOKEN [IGNORE_CASE]: {
          <DO: "do">
	| <THIS: "this">
        | <BODY: "body">
	| <DESCRIPTION: "description">
	| <DECLARATION: "declaration">
	| <TYPE: "type">
	| <IS: "is">
	| <CONSTANT: "constant">
	| <AND: "and">
        | <VARIABLE: "variable">  
	| <AS: "as">
	| <INTEGER: "integer">
	| <REAL: "real">
	| <STRING: "string">
	| <LOGIC: "logic">
	| <DESIGNATE: "designate">
	| <TO: "to">
	| <READ: "read">
	| <WRITE: "write">
	| <ALL: "all">
        | <REPEAT: "repeat">
        | <TRUE: "true">
        | <UNTRUE: "untrue">
        | <RESULT: "result">
        //------- isso pode vir a sair.. linguagem anterior ---------------------------
        | <NOT: "not">
        //-------- acaba aqui ---------------------------------------------------------
        | <AVALIATE: "avaliate">

}


//SPECIAL SYMBOLS
TOKEN : {
     < OPENBRACKET: "["> 
	| < CLOSEBRACKET: "]">
	| <DOT: ".">
	| < COMMA: ",">
	| < ASSIGNMENT: "=">
	| < PLUS: "+">
	| < MINUS: "-">
	| < MULTIPLY: "*">
	| < FRACTION: "/">
	| < POTENCY: "**">
	| < PERCENT: "%">
	| < MOD: "%%">
	| < EQUALS: "==">
        | < DIFFERENT: "!=">
	| < LESS: "<<">
	| < GREATER: ">>">
	| < LESSEQUAL: "<<=">
	| < GREATEREQUAL: ">>=">
	| < AMPERSAND: "&">	
	| < PIPE: "|">
	| < EXCLAMATION: "!">	
	| < OPENPARENTHESIS: "(">
	| < CLOSEPARENTHESIS: ")">

}

TOKEN : //Global
{
    < #LETTER_ID: ["A"-"Z", "a"-"z"] >
    | < #LETTER_UPPER: ["A"-"Z"] >
    | < #LETTER_LOWER: ["a"- "z"] >
    | < #DIGIT_ID: ["0"-"9"] >
    | < #SYMBOL_UND: ["_"] >
}

TOKEN : // Identificadores
{
    <IDENTIFICADOR: (<LETTER_UPPER>)(<SYMBOL_UND>)?((<LETTER_ID>(<SYMBOL_UND>)?)|(<DIGIT_ID>(<DIGIT_ID>)?(<LETTER_ID>|<SYMBOL_UND>|(<LETTER_ID>)<SYMBOL_UND>)))*>                    
}

TOKEN :
{
<IDENTIFICADOR_INVALIDO:
                                 (<DIGIT_ID>)+((<LETTER_ID>(<DIGIT_ID>)?(<DIGIT_ID>)?)|<SYMBOL_UND>(<DIGIT_ID>)?(<DIGIT_ID>)?)+ //COMECA COM DIGITO    
                                | ((<LETTER_ID>|<SYMBOL_UND>)(<DIGIT_ID>)?(<DIGIT_ID>)?)+ (<DIGIT_ID>)+ //TERMINA COM DIGITO
                                | (<LETTER_ID>|<SYMBOL_UND>)+ (<DIGIT_ID><DIGIT_ID><DIGIT_ID>) ((<LETTER_ID>)+|<SYMBOL_UND>|<DIGIT_ID>)* // DIGITOS CONSECUTIVOS
                                | (<LETTER_ID>|<SYMBOL_UND>|<DIGIT_ID>)* (<SYMBOL_UND><SYMBOL_UND>) (<LETTER_ID>|<SYMBOL_UND>|<DIGIT_ID>)* //SIMBOLOS CONSECUTIVOS
                                | (<SYMBOL_UND>)(<LETTER_ID>|<SYMBOL_UND>|<DIGIT_ID>)* // INICIANDO COM _     
                                | (<LETTER_LOWER>)(<LETTER_ID>|<SYMBOL_UND>|<DIGIT_ID>)* // INCIANDO COM MINUSCULA
                                

>  

}   

// CONSTANTE LITERAL TOKEN
TOKEN: {
   <CONSTANTE_LITERAL: "\"" (~[ "\"", "\n" ])* "\""> 
}


TOKEN :
{
    <CONSTANTE_NUM_INTEIRA_INVALIDA: ((<DIGIT>){4}(<DIGIT>)+) >
    | <CONSTANTE_NUM_REAL_INVALIDA:   ((<DIGIT>){5}(<DIGIT>)+)(<DOT>) (<DIGIT>)*
                                    | (((<DIGIT>){5})+(<DOT>)((<DIGIT>)(<DIGIT>)(<DIGIT>)+)*)
                                    | (((<DIGIT>){4})+(<DOT>)((<DIGIT>)(<DIGIT>)(<DIGIT>)+)*) 
                                    | (((<DIGIT>){3})+(<DOT>)((<DIGIT>)(<DIGIT>)(<DIGIT>)+)*) 
                                    | (((<DIGIT>){2})+(<DOT>)((<DIGIT>)(<DIGIT>)(<DIGIT>)+)*) 
                                    | ((<DIGIT>)+(<DOT>)((<DIGIT>)(<DIGIT>)(<DIGIT>)+)*) >

    | <CONSTANTE_LITERAL_INVALIDA: "\"" (~["\n","\r","\""])* ~["\""]
                                            | "\'" (~["\n","\r","\'"])* ~["\'"]|"'">
    
   // | <COMENTARIO_BLOCO_INVALIDO: "##" (~["#"])* >
   // | <COMENTARIO_BLOCO: "##" (~["#"])* "#">
}

TOKEN :
{
    < SIMBOLO_INVALIDO : (~ [
		"a"-"z",
		"A"-"Z",
		"0"-"9",
		"[",
		"]",
		"=",
		".",
		"/",
		"(",
		")",
		"+",
		"-",
		"*",
		"%",
		"=",
		"!",
		"<",
		">",
		"&",
		"|",
		"!",   
		" ",
		"\t",
		"\n",
		"\r",
		"\f"
    ])+ > 
}